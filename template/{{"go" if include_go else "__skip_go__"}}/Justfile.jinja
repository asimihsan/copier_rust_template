# Set up environment variables for CGO
_setup-env:
    #!/usr/bin/env bash
    set -e
    
    # Set up common variables
    TARGET_DIR=$(cargo metadata --format-version=1 --no-deps | jq -r '.target_directory')
    DEBUG_DIR="$TARGET_DIR/debug"

    # Set up common CGO flags
    echo "export CGO_LDFLAGS=\"-L${DEBUG_DIR} -l{{ project_slug_underscore }}_go\""

    if [[ "$OSTYPE" == "darwin"* ]]; then
        # macOS-specific flags
        SDK_PATH=$(xcrun --show-sdk-path)
        echo "export CGO_CFLAGS=\"-I${SDK_PATH}/usr/include\""
        echo "export CGO_LDFLAGS=\"$CGO_LDFLAGS -F${SDK_PATH}/System/Library/Frameworks -L${SDK_PATH}/usr/lib\""
        echo "export CGO_CPPFLAGS=\"-Wno-error -Wno-nullability-completeness -Wno-expansion-to-defined\""
        echo "export CC=\"$(xcrun -f clang)\""
    elif [[ "$OSTYPE" == "linux"* ]]; then
        # Linux-specific flags: ensure the shared library is found at runtime
        echo "export LD_LIBRARY_PATH=\"${DEBUG_DIR}:$LD_LIBRARY_PATH\""
    fi

# Generate C bindings using cbindgen
generate-bindings:
    #!/usr/bin/env bash
    set -e
    
    # Generate the bindings
    cbindgen --config cbindgen.toml --output binding.h

# Build the project
build: generate-bindings
    #!/usr/bin/env bash
    set -e
    
    eval "$(just _setup-env)"
    go build ./...

# Run tests
test: generate-bindings
    #!/usr/bin/env bash
    set -e
    
    eval "$(just _setup-env)"
    go test ./...

# Run linter
lint:
    #!/usr/bin/env bash
    set -e
    
    eval "$(just _setup-env)"
    golangci-lint run ./...

# Clean build artifacts
clean:
    go clean ./...
    rm -f binding.h
